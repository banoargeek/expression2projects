@name TTS_Togglable
@inputs 
@outputs HideChat
@persist [LS, Quee]:array IsValidLang:table TTSLang:string
@trigger 

#Credit to Jorca For TTS, Edited by Major, Fixes by Syvil.

#KEY: LS = LastSaid: The last thing the owner said in global chat.

#Initialization
runOnChat(1)
interval(1)
if (first() | dupefinished())
{
    IsValidLang = table(
    "en" = 1,
    "en-gb" = 1
    )
    HideChat = 0
    TTSLang = "en-gb"
}

#Chat Clk check and Variable Declaration
if (chatClk(owner()))
{
    LS = owner():lastSaid():lower():explode(" ") 
    
    #Chat Command Check and utilities
    if (LS[1,string] == "/tts" | LS[1,string] == "/t")
    {
        hideChat(1)
        
        #Check if the count is greater than 1, else toggle chat Visbility.
        if (LS:count() > 1)
        {
            if(LS[2,string] == "lang" | LS[2,string] == "l")
            {   
                if (IsValidLang[LS[3,string],number])
                {
                    TTSLang = LS[3,string]
                    print("TTS: Set TTS Language to "+TTSLang+"!") 
                }
                else
                {
                    print("TTS: Invalid Language Prefix!")
                }
            }
        }
        #Toggle Chat Visibility.
        else
        {
            HideChat = !HideChat
            if (HideChat)
            {
                print("TTS: Chat is Hidden!")
            }
            else
            {
                print("TTS: Chat is Visible!")
            }
        }
    }
    #If result is NOT a chat command, push to queue.
    else
    {
        Quee:pushString(LS:concat(" "))
    }

    #HideChat Check
    if (HideChat)
    {
        hideChat(1)
    }
}

#Stream Call and utilities
if (streamCanStart() & Quee:count() > 0)
{
    local S = Quee[1,string]
    Quee:shift()
    owner():streamStart(2,"https://translate.google.com/translate_tts?ie=UTF-8&tl="+TTSLang+"&client=tw-ob&q="+S)
}
